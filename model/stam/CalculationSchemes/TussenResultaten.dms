//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2020 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container TussenResultaten
{
	#include <RekenstapT.dms>
	#include <BebouwingsComponentT.dms>
	#include <GebouwOptieT.dms>

	template DoelLabelT
	{
		// begin case parameters
		unit<uint32> BebouwingsObject;
		parameter<Classifications/DoelLabel_NaSprong> DoelLabel_rel;
		// end case parameters

		unit<uint8>  SchilSprong_set  := VerbruiksOpties/SchilSprong_set; // outward ref
		unit<uint32> SchilSprong_x_MO := VerbruiksOpties/SchilSprong_set/xMO;

		parameter<Units/yr> Threshold_bouwnorm := 2000[Units/yr], Descr = "Bouwjaar vanaf welke geen sprongen meer gedaan kunnen worden";
		
		attribute<Classifications/SchilLabel> Van_rel_alt(BebouwingsObject) :=	
			BebouwingsObject/SchilLabel_rel != Classifications/schillabel/V/N ? BebouwingsObject/SchillabelOrDefault_rel :
			BebouwingsObject/Interpolate/Defaultlabel_lower;
			
		attribute<Classifications/SchilLabel> Van_rel    (BebouwingsObject) := BebouwingsObject/SchillabelOrDefault_rel;
		parameter<Classifications/SchilLabel> Naar_rel                      := Classifications/DoelLabel_NaSprong/SchilLabel_rel[DoelLabel_rel];
		
		attribute<SchilSprong_set> SchilSprong_rel    (BebouwingsObject) :=
			BebouwingsObject/SchilLabel_rel == Classifications/schillabel/V/N && BebouwingsObject/GeenLabel_Bouwjaar >= Threshold_bouwnorm ? (0/0)[SchilSprong_set]
			: rlookup(combine_data(Classifications/SchilSprongCode, Van_rel, Naar_rel), VerbruiksOpties/SchilSprong_set/SchilSprongCode_rel);
			
		attribute<SchilSprong_x_MO> SchilSprong_xMO_rel(BebouwingsObject) := combine_data(VerbruiksOpties/schilsprong_set/xMO, SchilSprong_rel, BebouwingsObject/Model_rel);

		parameter<bool> IsUtil  := BCname == 'BestaandeUtil'; 
		attribute<m2> 	cutoff (BebouwingsObject) := = IsUtil ? 'classifications/UtilTypeBestaand/isolatie_vast_cutoff[BebouwingsObject/BebouwingsType]' : 'const(0[m2], BebouwingsObject)'; //Hack voor woningen, want geen gedeeld domein BebouwingsType
		attribute<bool> IsCutoff (BebouwingsObject) := BebouwingsObject/Oppervlakte < cutoff ;
		
		attribute<Eur> Ki_GV_def (BebouwingsObject) := MakeDefined
		(
			IsCutoff 
			? SchilSprong_x_MO/Ki_cutoff_opp[SchilSprong_xMO_rel] * BebouwingsObject/Oppervlakte
			: SchilSprong_x_MO/Ki_asl[SchilSprong_xMO_rel] * BebouwingsObject/nrAansluitingen +
			 SchilSprong_x_MO/Ki_opp[SchilSprong_xMO_rel] * BebouwingsObject/Oppervlakte
			,0[Eur]
		);
		
		attribute<SchilSprong_set > SchilSprong_rel_alt     (BebouwingsObject) :=
			BebouwingsObject/SchilLabel_rel == Classifications/schillabel/V/N && BebouwingsObject/GeenLabel_Bouwjaar >= Threshold_bouwnorm ? (0/0)[SchilSprong_set]
			: rlookup(combine_data(Classifications/SchilSprongCode, Van_rel_alt, Naar_rel), VerbruiksOpties/SchilSprong_set/SchilSprongCode_rel);
		attribute<SchilSprong_x_MO> SchilSprong_xMO_rel_alt (BebouwingsObject) := combine_data(VerbruiksOpties/schilsprong_set/xMO, SchilSprong_rel_alt, BebouwingsObject/Model_rel);

		attribute<Eur> Ki_GV_alt (BebouwingsObject) := MakeDefined
		(
			IsCutoff
			? SchilSprong_x_MO/Ki_cutoff_opp[SchilSprong_xMO_rel_alt] * BebouwingsObject/Oppervlakte
			: SchilSprong_x_MO/Ki_asl[SchilSprong_xMO_rel_alt] * BebouwingsObject/nrAansluitingen +
			 SchilSprong_x_MO/Ki_opp[SchilSprong_xMO_rel_alt] * BebouwingsObject/Oppervlakte
			,0[Eur]
		);
		
		attribute<Eur> Ki_GV (BebouwingsObject) := 
			BebouwingsObject/SchilLabel_rel != Classifications/schillabel/V/N ? Ki_GV_def :
			(BebouwingsObject/Interpolate/R_lower * Ki_GV_alt + BebouwingsObject/Interpolate/R_upper * Ki_GV_def);
		
//		attribute<Eur>   Ki_GV_sum        (SchilSprong_set) := sum(Ki_GV, SchilSprong_rel);
//		attribute<Gj_yr> DeltaRV_sum      (SchilSprong_set) := sum(GebouwOpties/NaarD_w/Functioneel/DeltaRV, SchilSprong_rel);
		
// 		//CHECKS
// 		unit<uint32> BekendLabelBO := subset(BebouwingsObject/SchilLabel_rel != Classifications/schillabel/V/N)
// 		{		
// 			attribute<SchilSprong_set> Sprong (BekendLabelBO)   := SchilSprong_rel[nr_orgentity];
// 			attribute<Eur>   Ki_GV_bekend     (BekendLabelBO)   := Ki_GV[nr_orgentity];
// 			attribute<GJ_yr> DeltaRV_bekend   (BekendLabelBO)   := GebouwOpties/NaarD_w/Functioneel/DeltaRV[nr_orgentity];
// 			
// 			attribute<Eur>   Ki_GV_sum        (SchilSprong_set) := sum(Ki_GV_bekend, SchilSprong_rel[nr_orgentity]);
// 			attribute<Gj_yr> DeltaRV_sum      (SchilSprong_set) := sum(DeltaRV_bekend, SchilSprong_rel[nr_orgentity]);
// 		}
	}
}